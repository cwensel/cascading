import java.text.SimpleDateFormat

/*
 * Copyright (c) 2016-2019 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 * Copyright (c) 2007-2017 Xplenty, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 Using:

 To run a single test w/ gradle 1.10 and above, works with :test and :platformTest tasks
  > gradle :cascading-local:test --tests *LocalTapPlatformTest -i

 or a single method
  > gradle :cascading-local:test --tests *LocalTapPlatformTest.testIO -i

 To remotely debug use
   -Dtest.debug
*/

plugins {
  id "io.spring.bintray" version "0.11.1" apply false
}

apply from: 'etc/version.gradle'
apply from: "etc/s3Upload.gradle"

allprojects {

  apply plugin: 'idea'
  apply plugin: 'eclipse'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {url = 'https://repo.superflow.io/'}
    maven {url = 'https://repository.apache.org'}
  }
}

project.ext.distDir = null

task prepareDist() {
  project.distDir = mkdir( "${rootDir}/build/dist" )
}

ext.copyright = '<i>Copyright &#169; 2007-2017 Cascading Maintainers. All Rights Reserved.</i>'

subprojects {

  apply plugin: "io.spring.bintray"
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply from: '../etc/testing.gradle'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  group = 'net.wensel'
  version = releaseVersion

  // generate Java 9 module names
  ext.moduleName = name.replaceAll( '-', '.' )

  configurations {
    testArtifacts {
      extendsFrom testRuntimeClasspath
    }
  }

  bintray {
    bintrayUser = repoUserName
    bintrayKey = repoPassword

    org = 'wensel'
    repo =  isFinalRelease ? 'release' : 'wip'

    overrideOnUpload = true

    publication = 'maven'

    licenses = ['Apache-2.0']
    websiteUrl = 'http://cascading.org/'
    issueTrackerUrl = 'https://github.com/Cascading/cascading/issues'
    vcsUrl = 'https://github.com/Cascading/cascading.git'
  }

  task bintrayPublishBuild( dependsOn: [bintrayUpload, bintrayPublish]) {
  }

  jar {
    inputs.property( "moduleName", moduleName )
    manifest {
      attributes( 'Automatic-Module-Name': moduleName )
    }
  }

  javadoc.dependsOn << prepareDist

  javadoc {

    title = "Cascading ${releaseVersion}"
    destinationDir = file( "${distDir}/javadoc/${project.name}" )

    failOnError = false

    configure( options ) {
      linkSource = true
      encoding = "UTF8"
      bottom = copyright
      links = [
        'http://docs.oracle.com/javase/8/docs/api/',
        'http://junit.org/junit4/javadoc/4.11/',
        "http://${s3UploadDocs.destination}javadoc/cascading-core/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-local/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-hadoop2-common/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-hadoop2-io/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-hadoop2-mr1/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-hadoop2-tez/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-hadoop2-tez-stats/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-expression/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-nested/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-nested-json/".toString(),
        "http://${s3UploadDocs.destination}javadoc/cascading-platform/".toString()
      ]
    }
  }

  task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
  }

  task javadocJar( type: Jar, dependsOn: javadoc ) {
    from javadoc
    archiveClassifier = 'javadoc'
  }

  task testsJar( type: Jar, dependsOn: testClasses ) {
    from sourceSets.test.output
    archiveClassifier = 'tests'
  }

  task testSourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.test.allSource
    archiveClassifier = 'test-sources'
  }

  artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    archives testsJar
    archives testSourcesJar
    testArtifacts testsJar
    testArtifacts testSourcesJar
  }

  dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.11'
  }

  assemble.dependsOn << prepareDist

  assemble.doLast {

    if( project.name.equals( 'cascading-platform' ) )
      return

    copy {
      into "${distDir}/lib/${archivesBaseName}"
      from( configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.findAll {dep ->
        !dep.name.contains( 'cascading' )
      }.collect {dep ->
        dep.moduleArtifacts.collect {it.file}
      } )
    }

    copy {
      into distDir
      from configurations.default.allArtifacts.files
    }
  }

  publishing {
    publications {
      maven( MavenPublication ) {
        artifact sourcesJar
        artifact javadocJar
        artifact testsJar
        artifact testSourcesJar

        from components.java

        pom {
          name = 'cascading'
          description = 'An API for data management, analytics, and machine learning on parallel computing clusters.'
          url = 'http://cascading.wensel.net/'
          inceptionYear = '2007'
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          scm {
            url = 'https://github.com/cwensel/cascading.git'
          }
        }
      }
    }
  }
}

task allJavadoc( type: Javadoc, dependsOn: subprojects.javadoc ) {

  title = "Cascading ${releaseVersion}"
  destinationDir = file( "${distDir}/javadoc/all/" )

  source subprojects.javadoc.source
  classpath = rootProject.files( subprojects.javadoc.classpath )

  failOnError = false

  configure( options ) {
    linkSource = true
    encoding = "UTF8"
    bottom = copyright
    links = [
      'http://docs.oracle.com/javase/8/docs/api/',
      'http://junit.sourceforge.net/javadoc/'
    ]
  }
}

task prepareRelease( type: Copy, dependsOn: [':prepareDist',
                                             'cascading-core:build',
                                             'cascading-local:build',
                                             'cascading-hadoop2-common:build', 'cascading-hadoop2-io:build',
                                             'cascading-hadoop2-mr1:build',
                                             'cascading-hadoop2-tez:build', 'cascading-hadoop2-tez-stats:build',
                                             'cascading-expression:build',
                                             'cascading-nested:build', 'cascading-nested-json:build',
                                             'cascading-platform:build',
                                             ':allJavadoc'] ) {

  s3UploadDocs.dependsOn << prepareRelease
  s3UploadArtifacts.dependsOn << prepareRelease

  // copy dist and apply any analytics scripts
  def analytics = System.getProperty( 'publish.docs.analytics', '' )

  from "${distDir}/javadoc/"
  into "${s3UploadDocs.source}/javadoc"

  filter {line ->
    line.replaceAll( "</body>", "${analytics}\n</body>" )
  }
}

prepareRelease.doLast {
  logger.info( 'built all artifacts' )
}

task clean {
  doLast {
    delete buildDir
  }
}
task buildDist( dependsOn: prepareRelease ) {
  doLast {
    copy {
      from 'CHANGES.txt'
      from 'README.txt'
      from 'apl.txt'
      from 'LICENSE.txt'
      into distDir
    }
  }
}

task buildPackage( type: Tar, dependsOn: buildDist ) {

  description = "package current build, does not run tests"

  destinationDir = file( s3UploadArtifacts.source )
  baseName = "cascading-${releaseVersion}"
  compression = "GZIP"

  into( baseName ) {
    from distDir
  }
}

task buildLatestMeta( dependsOn: buildPackage ) {

  s3UploadArtifacts.dependsOn << buildLatestMeta

  ext.latestArchivePath = null
  ext.latestReleaseTagPath = null
  ext.latestPropertiesPath = null
  ext.latestJSONPath = null
}

buildLatestMeta.doLast {

  def df = new SimpleDateFormat( "yyyy-MM-dd'T'HH:mm'Z'" )
  df.setTimeZone( TimeZone.getTimeZone( "UTC" ) )
  String isoUTC = df.format( new Date() )

  def destination = s3UploadArtifacts.source
  def releaseTar = buildPackage.archivePath.name
  def releaseURL = "http://${s3UploadArtifacts.destination}${releaseTar}"

  latestArchivePath = new File( destination, 'latest.txt' )
  latestArchivePath.write( releaseURL )

  latestReleaseTagPath = new File( destination, 'latest-tag.txt' )
  latestReleaseTagPath.write( "${releaseTag}" )

  latestPropertiesPath = new File( destination, 'latest.properties' )
  latestPropertiesPath.write( "cascading.release.version=${releaseVersion}\n" +
    "cascading.release.major=${majorVersion}\n" +
    "cascading.release.minor=${minorVersion}\n" +
    "cascading.release.build=${buildNumber}\n" +
    "cascading.release.commit=${currentCommit}\n" +
    "cascading.release.tag=${releaseTag}\n" +
    "cascading.release.name=${releaseTar}\n" +
    "cascading.release.url=${releaseURL}\n" +
    "cascading.release.date=${isoUTC}"
  )

  latestJSONPath = new File( destination, 'latest.json' )
  latestJSONPath.write( """\
{
  "cascading": {
    "release": {
      "name": "${releaseTar}",
      "url": "${releaseURL}",
      "version": "${releaseVersion}",
      "major": "${majorVersion}",
      "minor": "${minorVersion}",
      "build": "${buildNumber}",
      "commit": "${currentCommit}",
      "tag": "${releaseTag}",
      "date": "${isoUTC}"
    },
    "docs": {
      "userguide": {
        "url": "http://${s3UploadDocs.destination}userguide/"
      },
      "api": {
        "url": "http://${s3UploadDocs.destination}javadoc/",
        "names": [
          "cascading-core",
          "cascading-local",
          "cascading-hadoop2-common",
          "cascading-hadoop2-io",
          "cascading-hadoop2-mr1",
          "cascading-hadoop2-tez",
          "cascading-hadoop2-tez-stats",
          "cascading-expression",
          "cascading-nested",
          "cascading-nested-json",
          "cascading-expression",
          "cascading-platform"
        ]
      }
    }
  }
}"""
  )
}
