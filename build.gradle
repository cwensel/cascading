/*
 * Copyright (c) 2007-2012 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
  Using:

  To run a single test:
    > gradle -Dtest.single=CoGroupFieldedPipesPlatformTest :src:local:test

  To remotely debug use
    -Dtest.debug

 */

apply from: 'version.gradle'

configurations {
  sshAntTask
  s3AntTask
}

dependencies {
  sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
  s3AntTask 'thirdparty:awstasks:0.3'
}

repositories {
  mavenLocal()
  mavenCentral()
  mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
  mavenRepo name: 'apache', url: 'https://repository.apache.org/content/repositories/releases/'
}

subprojects { subproject ->
  if( subproject.name != 'src' )
  {
    apply plugin: 'java'
    apply plugin: 'maven'

    buildDir = "${rootDir}/build"
    group = 'cascading'
    version = releaseVersion
    platformVersion = releasePlatformVersion

    configurations {
      sshAntTask
    }

    repositories {
      mavenLocal()
      mavenCentral()
      mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
      mavenRepo name: 'apache', url: 'https://repository.apache.org/content/repositories/releases/'
    }

    dependencies {
      sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
    }

    task repoUpload( dependsOn: jar ) {
      repoURL = 'repo@conjars.org:'
      repoPort = '22'
      repoKeyfile = System.properties[ 'repo.keyfile' ]
      repoKeyfilePassphrase = ''
    }

    repoUpload << {

      pom {
        artifactId = "cascading-${artifactId}"

        whenConfigured { pom ->
          pom.dependencies.findAll { dep ->
            dep.groupId == 'cascading'
          }.each { dep ->
            dep.artifactId = "cascading-${dep.artifactId}"
          }
        }

        project {
          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }
        }
      }.writeTo( "${buildDir}/poms/${project.name}/pom.xml" )

      ant.taskdef( name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
              classpath: configurations.sshAntTask.asPath )

      assert repoKeyfile

      // conjars expects to see no directories, just files
      ant.scp( todir: repoURL,
              keyfile: repoKeyfile, passphrase: repoKeyfilePassphrase, port: repoPort, trust: 'true' ) {

        fileset( dir: "${buildDir}/poms/${project.name}" ) {
          name = "pom.xml"
        }

        fileset( dir: "${buildDir}/libs/" ) {
          name = "cascading-${project.name}-${releaseVersion}.jar"
        }

        fileset( dir: "${buildDir}/libs/" ) {
          name = "cascading-${project.name}-${releaseVersion}-sources.jar"
        }
      }
    }

    task ideLibs( type: Sync ) {
      description = 'Copies the dependencies to the lib folder for use in an ide.'
      from {
        configurations.runtime.files { dep -> !( dep instanceof ProjectDependency ) }
      }
      into "${rootDir}/lib/${subproject.name}"
    }

    javadoc.onlyIf { false }
  }
}

task javadocAll( type: Javadoc, dependsOn: [ 'src:core:assemble', 'src:local:assemble', 'src:hadoop:assemble', 'src:xml:assemble', 'src:test:assemble' ] ) {

  def nonSrcSubprojects = subprojects.findAll { ![ 'src', 'test' ].contains( it.name ) }
  source nonSrcSubprojects.collect { it.sourceSets.main.allJava }

  source 'src/test/cascading/CascadingTestCase.java'
  source 'src/test/cascading/PlatformTestCase.java'
  source 'src/test/cascading/tuple/TupleListCollector.java'
  source 'src/test/cascading/test/TestPlatform.java'
  source 'src/test/cascading/test/LocalPlatform.java'
  source 'src/test/cascading/test/HadoopPlatform.java'

  copyright = '<i>Copyright &#169; 2007-2011 Concurrent, Inc. All Rights Reserved.</i>'
  title = "Cascading ${releaseVersion}"

  classpath = files( nonSrcSubprojects.collect { subproject -> subproject.sourceSets.main.compileClasspath } )
  classpath = files( childProjects[ 'src' ].childProjects[ 'test' ].sourceSets.main.compileClasspath )

  destinationDir = new File( rootDir, 'build/docs/javadoc' )

  configure( options ) {
    encoding = "UTF8"
    bottom = copyright
    links = [
            'http://java.sun.com/javase/6/docs/api/',
            'http://dist.codehaus.org/janino/javadoc/',
            'http://hadoop.apache.org/common/docs/r0.20.2/api/'
    ]
  }
}

task buildDist( dependsOn: javadocAll ) << {
  def distDir = mkdir( "${rootDir}/build/dist" )

  copy {
    from 'CHANGES.txt'
    from 'README.txt'
    from 'apl.txt'
    from 'LICENSE.txt'
    into distDir
  }

  copy {
    from javadocAll.destinationDir
    into "${distDir}/javadoc"
  }
}

task buildPackage( type: Tar, dependsOn: buildDist ) {
  description = "package current build, does not run tests"

  def distDir = mkdir( "${rootDir}/build/dist" )

  destinationDir = buildDir
  baseName = "cascading-${releaseVersion}"
  compression = "GZIP"

  into( baseName ) {
    from distDir
  }
}

task prepareRelease( dependsOn: [ 'src:core:build', 'src:local:build', 'src:hadoop:build', 'src:xml:build', 'src:test:build' ] )

prepareRelease << {
  logger.info( 'built all artifacts' )
}

task repoRelease( dependsOn: [ prepareRelease, 'src:core:repoUpload', 'src:local:repoUpload', 'src:hadoop:repoUpload', 'src:xml:repoUpload', 'src:test:repoUpload' ] )

repoRelease << {
  logger.info( 'uploaded all artifacts' )
}

task buildLatestMeta( dependsOn: buildPackage ) {

  latestArchivePath = null
  latestReleaseTagPath = null
}

buildLatestMeta << {

  def publishBucket = System.properties[ 'publish.bucket' ]
  def releaseTar = buildPackage.archivePath.name

  latestArchivePath = new File( buildDir, 'latest.txt' )
  latestArchivePath.write( "http://${publishBucket}/cascading/${majorVersion}/${releaseTar}" )

  latestReleaseTagPath = new File( buildDir, 'latest-tag.txt' )
  latestReleaseTagPath.write( "${releaseTag}" )
}

task s3Upload( dependsOn: buildLatestMeta ) {

  awsAccessId = System.properties[ 'publish.aws.accessId' ]
  awsSecretKey = System.properties[ 'publish.aws.secretKey' ]
  s3Bucket = System.properties[ 'publish.bucket' ]

  remotePath = "cascading/${majorVersion}/"

  archive = buildPackage.archivePath
}

s3Upload << {

  ant.taskdef( name: 's3Upload', classname: 'dak.ant.taskdefs.S3Upload',
          classpath: configurations.s3AntTask.asPath )

  ant.s3Upload( verbose: 'true', accessId: awsAccessId, secretKey: awsSecretKey,
          bucket: s3Bucket, prefix: remotePath, publicRead: 'true' ) {
    fileset( file: archive )
    fileset( file: buildLatestMeta.latestArchivePath )
    fileset( file: buildLatestMeta.latestReleaseTagPath )
  }
}

task sitePublish( dependsOn: s3Upload ) << {

  ant.taskdef( name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
          classpath: configurations.sshAntTask.asPath )

  def publishDownloadPath = System.properties[ 'publish.download.path' ]
  def publishPort = !System.properties[ 'publish.port' ] ? '22' : System.properties[ 'publish.port' ]
  def publishKeyFile = System.properties[ 'publish.keyfile' ]

  def remoteToLatestFile = "${publishDownloadPath}/cascading/${majorVersion}/latest.txt"
  def remoteToLatestReleaseTagFile = "${publishDownloadPath}/cascading/${majorVersion}/latest-tag.txt"

  ant.scp( file: buildLatestMeta.latestArchivePath, remoteToFile: remoteToLatestFile,
          keyfile: publishKeyFile, passphrase: '', port: publishPort, trust: 'true' )

  ant.scp( file: buildLatestMeta.latestReleaseTagPath, remoteToFile: remoteToLatestReleaseTagFile,
          keyfile: publishKeyFile, passphrase: '', port: publishPort, trust: 'true' )
}
