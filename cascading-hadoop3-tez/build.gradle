/*
 * Copyright (c) 2016-2022 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 * Copyright (c) 2007-2017 Xplenty, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: '../etc/testingPlatform.gradle'

ext.hadoop3Version = '3.3.1'
ext.tezVersion = '0.10.1'

if ( System.properties [ 'hadoop3.release.final'] )
  hadoop3Version = System.properties[ 'hadoop3.release.final' ]

if ( System.properties [ 'tez.release.final'] )
  tezVersion = System.properties[ 'tez.release.final' ]

repositories {
  if( tezVersion.contains( 'SNAPSHOT' ) )
    maven {url = 'https://repository.apache.org/content/repositories/snapshots'}
}

dependencies {

  api project( ':cascading-core' )
  api project( ':cascading-hadoop3-common' )
  api project( ':cascading-hadoop3-io' )
  api project( ':cascading-hadoop3-tez-stats' )

  compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.1'

  // forces update of yarn slf4j dependencies
  compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
  compileOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.32'

  compileOnly( group: 'org.apache.tez', name: 'tez-api', version: tezVersion )
  compileOnly( group: 'org.apache.tez', name: 'tez-dag', version: tezVersion )
  compileOnly( group: 'org.apache.tez', name: 'tez-mapreduce', version: tezVersion )

  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoop3Version )
  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoop3Version )
  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: hadoop3Version )
  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-aws', version: hadoop3Version )


  testImplementation project( path: ':cascading-platform', configuration: 'testArtifacts' )
  testImplementation project( path: ':cascading-hadoop3-common', configuration: 'testArtifacts' )

  testImplementation( group: 'org.apache.tez', name: 'tez-tests', version: tezVersion, classifier: 'tests' )
  testImplementation( group: 'org.apache.tez', name: 'tez-api', version: tezVersion )
  testImplementation( group: 'org.apache.tez', name: 'tez-dag', version: tezVersion )
  testImplementation( group: 'org.apache.tez', name: 'tez-yarn-timeline-history-with-acls', version: tezVersion )
  testImplementation( group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: hadoop3Version )
}

configurations.all {
  resolutionStrategy {
    eachDependency {def details ->
      if( details.requested.group == 'org.apache.hadoop' )
        details.useVersion hadoop3Version
    }
  }
}

test {

  systemProperties[ 'test.platform.includes' ] = 'hadoop3-tez'

  delete( "target" ) // hardcoded path in hadoop3

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

platformTest {

  systemProperties[ 'test.platform.includes' ] = 'hadoop3-tez'

  delete( "target" ) // hardcoded path in hadoop3

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

javadoc {

  classpath = files( project.sourceSets.test.compileClasspath )

  configure( options ) {
    links << "http://hadoop.apache.org/docs/r${hadoop3Version}/api/".toString()
    links << "http://tez.apache.org/releases/${tezVersion}/tez-api-javadocs/".toString()
  }
}
