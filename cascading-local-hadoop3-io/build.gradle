/*
 * Copyright (c) 2016-2022 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: '../etc/testingPlatform.gradle'

ext.hadoop3Version = '3.3.1'

if ( System.properties [ 'hadoop3.release.final'] )
  hadoop3Version = System.properties[ 'hadoop3.release.final' ]

dependencies {
  api project( ':cascading-local' )
  api project( ':cascading-hadoop3-io' )

  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoop3Version )
  compileOnly( group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoop3Version )

  testImplementation project( ':cascading-hadoop3-mr' )
  testImplementation project( path: ':cascading-platform', configuration: 'testArtifacts' )
  testImplementation project( path: ':cascading-hadoop3-mr', configuration: 'testArtifacts' )
}

test {

  delete( "target" ) // hardcoded path in hadoop3

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

platformTest {
  runPlatformTests = false

  delete( "target" ) // hardcoded path in hadoop3

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

javadoc {

  classpath = files( project.sourceSets.test.compileClasspath )

  configure( options ) {
    links << "http://hadoop.apache.org/docs/r${hadoop3Version}/api/".toString()
  }
}
