/*
 * Copyright (c) 2016-2017 Chris K Wensel <chris@wensel.net>. All Rights Reserved.
 * Copyright (c) 2007-2017 Xplenty, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext.hadoop2Version = '2.7.3'

if( System.properties[ 'hadoop2mr1.release.final' ] )
  hadoop2Version = System.properties[ 'hadoop2mr1.release.final' ]

dependencies {

  compile project( ':cascading-core' )
  compile project( ':cascading-hadoop2-common' )
  compile project( ':cascading-hadoop2-io' )

  // forces update of yarn slf4j dependencies
  provided group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
  provided group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'

  testCompile project( path: ':cascading-core', configuration: 'testArtifacts' )
  testCompile project( path: ':cascading-hadoop2-common', configuration: 'testArtifacts' )
  testCompile project( path: ':cascading-platform', configuration: 'testArtifacts' )

  provided( group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: hadoop2Version )
  provided( group: 'org.apache.hadoop', name: 'hadoop-common', version: hadoop2Version )

  testCompile( group: 'org.apache.hadoop', name: 'hadoop-minicluster', version: hadoop2Version )
}

configurations.all {
  resolutionStrategy {

    if( !hadoop2Version.startsWith( '2.4' ) )
      force 'com.google.guava:guava:11.0.2'
  }
}

test {

  delete( "target" ) // hardcoded path in hadoop2

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

platformTest {

  delete( "target" ) // hardcoded path in hadoop2

  forkEvery = 1 // static fields on the platform test get munged otherwise

  systemProperties[ 'hadoop.log.dir' ] = new String( "${buildDir}/test/log" )
  systemProperties[ 'hadoop.tmp.dir' ] = new String( "${buildDir}/test/tmp" )
  systemProperties[ 'test.build.data' ] = new String( "${buildDir}/test/data" )
}

javadoc {

  source 'src/test/java/cascading/platform/hadoop2/Hadoop2MR1Platform.java'

  classpath = files( project.sourceSets.test.compileClasspath )

  configure( options ) {
    links << 'http://hadoop.apache.org/docs/r2.2.0/api/'
    links << "http://${rootProject.s3UploadDocs.destination}javadoc/cascading-core".toString()

//    linksOffline( '../../../cascading-core', "http://${rootProject.s3UploadDocs.destination}javadoc/cascading-core".toString() )
  }
}